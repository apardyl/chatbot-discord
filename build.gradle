buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

group 'com.pardyl.chatbot'
version '1.3.5.1'

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'application'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.pardyl.chatbot:core:1.3.5'
    compile 'net.dv8tion:JDA:3.7.1_392'
    compile 'ch.qos.logback:logback-classic:1.2.3'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

mainClassName = 'com.pardyl.chatbot.discord.Application'

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "apardyl"
            name "Adam Pardyl"
            email "adam@pardyl.com"
        }
    }

    scm {
        url "https://github.com/apardyl/chatbot-discord"
    }
}

publishing {
    publications {
        bintrayMaven(MavenPublication) {
            from components.java
            groupId 'com.pardyl.chatbot'
            artifactId 'discord'
            artifact sourcesJar
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'chatbot discord backend')
                root.appendNode('name', 'chatbot-discord')
                root.appendNode('url', 'https://github.com/apardyl/chatbot-discord')
                root.children().last() + pomConfig
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from 'src/main/kotlin'
}

bintray {
    user = 'apardyl'
    key = 'key'
    publications = ['bintrayMaven']

    pkg {
        repo = 'maven'
        name = 'chatbot-discord'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/apardyl/chatbot-discord.git'
        version {
            name = project.version
        }
    }
}
